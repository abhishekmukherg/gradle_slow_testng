apply plugin: 'java'
apply plugin: 'idea'

sourceSets {
    testng
}

configurations {
    testngCompile.extendsFrom compile
    testngRuntime.extendsFrom testngCompile, runtime
}

dependencies {
    testngCompile 'org.testng:testng:6.8.8'
    testngCompile sourceSets.main.output
}

repositories {
    mavenCentral()
}

def generatedSources = "gen/main"

task generateMainClasses {
    outputs.dir generatedSources
    doLast {
        [(1..10), (1..10), (1..10), (1..10)].combinations().each {
                rootNum, p1, p2, classNum ->
            def file = file("$generatedSources/a$rootNum/b${p1}/c${p2}/Z${classNum}.java")
            file.parentFile.mkdirs()
            file.write("""package a${rootNum}.b${p1}.c${p2};

            public class Z${classNum} {
                public int classNum;
                public Z${classNum}() {
                    this.classNum = ${classNum};
                }
            }
            """)
        }
    }
}

compileJava.source generateMainClasses.outputs.files, sourceSets.main.java
clean.dependsOn cleanGenerateMainClasses

task testng(type: Test) {
    useTestNG() {
        listeners << 'testing.Listener'
        suites file('src/testng/resources/unit.xml')
    }
    testClassesDir = sourceSets.testng.output.classesDir
    classpath = sourceSets.testng.runtimeClasspath
}
